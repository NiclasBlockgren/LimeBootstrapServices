@ECHO OFF

IF [%1]==[] (
	ECHO Version missing
	exit /b
)

SET PYTHON_VERSION_STRING_2=2.7.5
SET PYTHON_VERSION_STRING_3=3.3.4
SET PYTHON_DRIVE=C:

SET SELECTED_VERSION=%1
SET PYTHON_VERSION=0
IF %SELECTED_VERSION%==2 (SET PYTHON_VERSION=%PYTHON_VERSION_STRING_2%)
IF %SELECTED_VERSION%==3 (SET PYTHON_VERSION=%PYTHON_VERSION_STRING_3%)
IF %PYTHON_VERSION%==0 (ECHO Invalid version && exit /b)
SET PYTHON_MSI_32=%~dp0python-%PYTHON_VERSION%.msi

REM Remove '.' in the version number (3.3.3 -> 333),
REM and keep the two leftmost digits (334 -> 33)
SET PYTHON_FOLDER_VERSION=%PYTHON_VERSION:.=%
SET PYTHON_FOLDER_VERSION=%PYTHON_FOLDER_VERSION:~0,2%
SET PYTHON_MAJOR_VERSION=%PYTHON_FOLDER_VERSION:~0,1%
SET PYTHON_MINOR_VERSION=%PYTHON_FOLDER_VERSION:~1,1%

REM Build the python installation path
SETLOCAL ENABLEDELAYEDEXPANSION

SET PYTHON_PATH=%PYTHON_DRIVE%\Python%PYTHON_FOLDER_VERSION%
SET ACTUAL_PYTHON_MSI=%PYTHON_MSI_32%

ECHO Installing python %PYTHON_VERSION%
CALL msiexec /i !ACTUAL_PYTHON_MSI!

CALL %PYTHON_PATH%\Python.exe get-pip.py
CALL %PYTHON_PATH%\Scripts\pip install virtualenv
CALL %PYTHON_PATH%\Scripts\pip install virtualenvwrapper-win

ENDLOCAL
