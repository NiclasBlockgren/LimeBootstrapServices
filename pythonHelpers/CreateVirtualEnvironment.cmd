@ECHO OFF

IF [%1]==[] (
	ECHO Version missing
	exit /b
)

SET PYTHON_VERSION_STRING_2=2.7.5
SET PYTHON_VERSION_STRING_3=3.3.4
SET PYTHON_DRIVE=C:

SET SELECTED_VERSION=%1
SET SELECTED_ENV_NAME=%2
SET SELECTED_PROJECT_PATH=%3

SET PYTHON_VERSION=0
IF %SELECTED_VERSION%==2 (SET PYTHON_VERSION=%PYTHON_VERSION_STRING_2%)
IF %SELECTED_VERSION%==3 (SET PYTHON_VERSION=%PYTHON_VERSION_STRING_3%)
IF %PYTHON_VERSION%==0 (ECHO Invalid version && exit /b)
SET PYTHON_EXE=Python.exe

REM Remove '.' in the version number (3.3.3 -> 333),
REM and keep the two leftmost digits (334 -> 33)
SET PYTHON_FOLDER_VERSION=%PYTHON_VERSION:.=%
SET PYTHON_FOLDER_VERSION=%PYTHON_FOLDER_VERSION:~0,2%
SET PYTHON_MAJOR_VERSION=%PYTHON_FOLDER_VERSION:~0,1%
SET PYTHON_MINOR_VERSION=%PYTHON_FOLDER_VERSION:~1,1%

REM Build the python installation path
SETLOCAL ENABLEDELAYEDEXPANSION
SET PYTHON_PATH=%PYTHON_DRIVE%\Python%PYTHON_FOLDER_VERSION%

CALL %PYTHON_PATH%\mkvirtualenv.bat -p "%PYTHON_PATH%\%PYTHON_EXE%" %SELECTED_ENV_NAME%
CALL %PYTHON_PATH%\setprojectdir.bat %SELECTED_PROJECT_PATH%
CALL %PYTHON_PATH%\workon.bat %SELECTED_ENV_NAME%

ENDLOCAL



